buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.10'
  }
}


plugins {
  id 'org.springframework.boot' version '2.1.8.RELEASE'
  id 'io.spring.dependency-management' version '1.0.8.RELEASE'
  id 'java'
  id 'com.google.protobuf' version "0.8.10"
  id "io.github.kobylynskyi.graphql.codegen" version "1.2.2"
}

group = 'grpc-graphql'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '12'

repositories {
  mavenCentral()
}

dependencies {
  compile 'io.github.lognet:grpc-spring-boot-starter:3.4.1'
  compile 'com.graphql-java-kickstart:graphiql-spring-boot-starter:5.2'

  implementation 'com.graphql-java:graphql-java:13.0'
  implementation 'com.graphql-java:graphql-java-spring-boot-starter-webmvc:1.0'
  implementation 'org.springframework.boot:spring-boot-starter-web'
}

sourceSets {
  main {
    java {
      srcDirs = ['src/main/java', 'src/main/protoGen', 'src/main/graphqlGen']
    }
  }
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.5.1'
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:1.23.0"
    }
  }

  generateProtoTasks {
    ofSourceSet('main').each { task ->
      task.builtins {
        java {
          outputSubDir = 'protoGen'
        }
      }
      task.plugins {
        grpc {
          outputSubDir = 'protoGen'
        }
      }
    }
  }
  generatedFilesBaseDir = "$projectDir/src/"
}

task cleanProtoGen {
  doFirst {
    delete("$projectDir/src/main/protoGen")
  }
}

task cleanGraphqlGen {
  doFirst {
    delete("$projectDir/src/main/graphqlGen")
  }
}

clean {
  dependsOn 'cleanProtoGen'
  dependsOn 'cleanGraphqlGen'
}

compileJava.dependsOn graphqlCodegen

graphqlCodegen {
  graphqlSchemaPaths = ["$projectDir/src/main/resources/schema.graphqls".toString()]
  outputDir = new File("$projectDir/src/main/graphqlGen")
  apiPackageName = "grpcgraphql.graphql.api"
  modelPackageName = "grpcgraphql.graphql.model"
  customTypesMapping = [
        DateTime: "java.util.Date"
  ]
  modelNameSuffix = "TO"
  generateApis = true
}